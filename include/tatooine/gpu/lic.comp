#version 450
//==============================================================================
layout(local_size_x = 32, local_size_y = 32) in;
layout(binding = 0, rgba32f) uniform image2D lic_tex;

uniform sampler2D noise_tex;
uniform sampler2D v_tex;

uniform uvec2     noise_tex_res;
uniform uvec2     v_tex_res;
uniform vec2      domain_min;
uniform vec2      domain_max;
uniform uint      num_samples;
uniform float     stepsize;
uniform float     nan;
//==============================================================================
vec2 idx_to_uv(uvec2 image_pos, uvec2 res) {
  // calculate normalized texture coordinates
  return ((image_pos / vec2(res - 1) * (res * 2 - 2)) + 1) / vec2(res * 2);
}
//------------------------------------------------------------------------------
vec2 domain_pos_to_uv(vec2 domain_pos, vec2 min, vec2 max, uvec2 res) {
  const vec2 grid_pos = (domain_pos - min) / (max - min);
  return (grid_pos * (res * 2 - 2) + 1) / (vec2(res * 2));
}
//------------------------------------------------------------------------------
vec2 idx_to_domain_pos(uvec2 image_pos, vec2 min, vec2 max) {
  return image_pos / vec2(imageSize(lic_tex) - 1) * (max - min) + min;
}
//------------------------------------------------------------------------------
vec2 v(vec2 pos) {
  return normalize(
      texture(v_tex, domain_pos_to_uv(pos, domain_min, domain_max, v_tex_res))
          .rg);
}
//------------------------------------------------------------------------------
float noise(vec2 pos) {
  vec2 fres      = vec2(imageSize(lic_tex));
  vec2 fnoiseres = vec2(noise_tex_res);
  return texture(noise_tex,
                 domain_pos_to_uv(pos, domain_min, domain_max, noise_tex_res) *
                     fres / fnoiseres)
      .r;
}

//------------------------------------------------------------------------------
vec2 rungekutta4_step(vec2 pos, float stepsize) {
  const vec2 k1 = stepsize * v(pos);

  const vec2 x2 = pos + k1 / 2;
  if (x2.x < domain_min.x || x2.x > domain_max.x ||
      x2.y < domain_min.y || x2.y > domain_max.y) {
    return vec2(nan, nan);
  }
  const vec2 k2 = stepsize * v(x2);

  const vec2 x3 = pos + k2 / 2;
  if (x3.x < domain_min.x || x3.x > domain_max.x ||
      x3.y < domain_min.y || x3.y > domain_max.y) {
    return vec2(nan, nan);
  }
  const vec2 k3 = stepsize * v(x3);

  const vec2 x4 = pos + k3;
  if (x4.x < domain_min.x || x4.x > domain_max.x ||
      x4.y < domain_min.y || x4.y > domain_max.y) {
    return vec2(nan, nan);
  }
  const vec2 k4 = stepsize * v(x4);

  const vec2 stepped = pos + (k1 + 2 * k2 + 2 * k3 + k4) / 6;
  if (stepped.x < domain_min.x || stepped.x > domain_max.x ||
      stepped.y < domain_min.y || stepped.y > domain_max.y) {
    return vec2(nan, nan);
  }
  return stepped;
}
//==============================================================================
void main() {
  const uvec2 lic_tex_res = uvec2(imageSize(lic_tex));
  const uvec2 idx = uvec2(gl_GlobalInvocationID.xy);
  if (idx.x >= lic_tex_res.x || idx.y >= lic_tex_res.y) { return; }
  const vec2 pos = idx_to_domain_pos(idx, domain_min, domain_max);
  float      out_val = noise(pos);
  uint       cnt     = 1;

  // forward integration
  vec2 pos_integrated = pos;
  for (uint i = 0; i < num_samples; ++i) {
    pos_integrated = rungekutta4_step(pos_integrated, stepsize);
    if (isnan(pos_integrated.x) || isnan(pos_integrated.y)) {
      break;
    } else {
      out_val += noise(pos_integrated);
      ++cnt;
    }
  }

  // backward integration
  pos_integrated = pos;
  for (uint i = 0; i < num_samples; ++i) {
    pos_integrated = rungekutta4_step(pos_integrated, -stepsize);
    if (isnan(pos_integrated.x) || isnan(pos_integrated.y)) {
      break;
    } else {
      out_val += noise(pos_integrated);
      ++cnt;
    }
  }

  // contrast stretch
  out_val /= cnt;
  out_val = clamp(out_val * 3 - 0.8, 0, 1);

  imageStore(lic_tex, ivec2(idx), vec4(out_val, out_val, out_val, 1));
}
